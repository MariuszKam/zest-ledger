plugins {
	id 'java'
	id 'com.diffplug.spotless' version '7.2.1'
	id 'jacoco'
	id 'info.solidsoft.pitest' version '1.19.0-rc.1'
}

group = 'io.zestledger'
version = '1.0-SNAPSHOT'

repositories {
	mavenCentral()
}

spotless {
	format 'misc', {
		target '*.md', '.gitignore', '*.yml', '*.yaml', '*.gradle', '*.txt'
		trimTrailingWhitespace()
		leadingSpacesToTabs()
		endWithNewline()
	}
	java {
		googleJavaFormat('1.28.0').aosp()
		formatAnnotations()
		importOrder()
		removeUnusedImports()
	}
}

jacoco {
	toolVersion = "0.8.13"
	reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

dependencies {
	testImplementation platform('org.junit:junit-bom:5.10.0')
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}
jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.85
			}
		}
	}
}

tasks.register('jacocoTest') {
	dependsOn 'test', 'jacocoTestReport', 'jacocoTestCoverageVerification'
}

pitest {
	junit5PluginVersion = '1.2.1'
	mutators = ['STRONGER']
	timeoutConstInMillis = 4000
	timestampedReports = false
	mutationThreshold = 70
	targetClasses = ['io.zestledger.*']
	threads = 4
	outputFormats = ['HTML', 'XML']
	failWhenNoMutations = false
}
